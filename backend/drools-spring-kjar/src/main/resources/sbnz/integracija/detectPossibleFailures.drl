package sbnz.integracija;

import sbz.cardiagnosticbe.model.drools.DetectFailureParameters;
import sbz.cardiagnosticbe.model.drools.PossibleFailuresList;
import sbz.cardiagnosticbe.model.drools.PossibleFailure;
import sbz.cardiagnosticbe.model.db.Failure;
import sbz.cardiagnosticbe.model.db.Indicator;

import java.util.Set;
import java.util.List;
import java.lang.Long;

rule "Detect possible failures"
    lock-on-active true
    agenda-group "detect-failures"
    when
        $possibleFailures: PossibleFailuresList()
        $vi: DetectFailureParameters(
            $vehicleModelId: vehicleModelId,
            $vehicleProductionYear: vehicleProductionYear,
            $visibleIndicators: indicators
        )
        $f: Failure(
            $fid: id,
            $code: DTC,
            $failureIndicators: indicators
        )
        filter($fid, $failureIndicators, $visibleIndicators, $vehicleModelId, $vehicleProductionYear;)
        Number($matchingIndicators: intValue > 0) from accumulate(
                Object(
                    $x: this,
                    this memberOf $failureIndicators
                ) from $visibleIndicators,
                count($x)
            )
    then
        System.out.println(String.format("Filtered failure %s - number of matching indicators: %s", $code, $matchingIndicators));
        PossibleFailure possibleFailure = new PossibleFailure($f, $matchingIndicators);
        $possibleFailures.getFailures().add(possibleFailure);

        insert(possibleFailure);
        update($possibleFailures);
end

query filter(Long id, Set failureAllIndicators, Set visibleIndicators, long vehicleModelId, int vehicleProductionYear)
    genericDtcCode(id;) or matchVehicleModelCode(id, vehicleModelId, vehicleProductionYear;)
end

query genericDtcCode(Long _id)
    Failure(
        id == _id,
        manufacturerSpecific == false
    )
end

query matchVehicleModelCode(Long _id, long _vehicleModelId, int _vehicleProductionYear)
    Failure(
        _id == id,
        manufacturerSpecific == true,
        vehicleInformation.(
            vehicleModel.(
                id == _vehicleModelId
            ),
            minVehicleProductionYear <= _vehicleProductionYear,
            maxVehicleProductionYear >= _vehicleProductionYear
        )
    )
end
