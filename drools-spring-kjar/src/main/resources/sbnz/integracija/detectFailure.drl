package sbnz.integracija;

import sbz.cardiagnosticbe.model.drools.VisibleIndicators;
import sbz.cardiagnosticbe.model.drools.FailureList;
import sbz.cardiagnosticbe.model.Failure;
import sbz.cardiagnosticbe.model.Indicator;

import java.util.Set;
import java.util.List;

rule "Detect failures"
    agenda-group "detect-failure"
    when
        $result: FailureList();
        $s: VisibleIndicators($visibleIndicators: indicators, $userCarState: carState);
        $f: Failure($failureIndicators: indicators, carState == $userCarState)
        $visibleIndicatorsIds: Set() from accumulate (
            Indicator($id: id) from $visibleIndicators, collectSet($id)
        )
        checkFailureIndicators($failureIndicators, $visibleIndicatorsIds;);
    then
        $result.getFailures().add($f);
end

query checkFailureIndicators(Set failureAllIndicators, Set visibleIndicatorsIds)
    $cnt: Number(intValue == failureAllIndicators.size()) from accumulate(
        Indicator($id: id, id memberOf visibleIndicatorsIds) from failureAllIndicators,
        init(int count = 0;),
        action(count += 1;),
        result(count)
    )
end
